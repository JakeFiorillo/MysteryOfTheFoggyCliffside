// main global script file

//=============================================================================
// INITIALIZATION
//=============================================================================

// set default game options
function set_default_options()
{
  System.Volume = 100;
  sldAudio.Value = System.Volume;
  sldSpeed.Value = 40;
  SetGameSpeed(40);

  if (IsSpeechVoxAvailable())
  {
     Speech.VoiceMode = eSpeechVoiceAndText;
     btnVoice.Text = "Voice and Text";
     sldVoice.Value = 255;
     SetSpeechVolume(255);
  }

  if (System.SupportsGammaControl)
  {
    System.Gamma = 100;
    sldGamma.Value = 100;
  }
}

// initialize gPanel
function initialize_control_panel()
{
  if (!IsSpeechVoxAvailable())
  {
    // hide the speech controls, if speech is not available
    lblVoice.Visible = false;
    btnVoice.Visible = false;
    sldVoice.Visible = false;
  }

  if (!System.SupportsGammaControl) {
    // hide the gamma control, if not supported
    sldGamma.Visible = false;
    lblGamma.Visible = false;
  }

  set_default_options();
}

// called when the game starts, before the first room is loaded
function game_start()
{
  // initialize gPanel controls
  initialize_control_panel();

  // set KeyboardMovement movement mode
  KeyboardMovement.Mode = eKeyboardMovementModeTapping;

  // set KeyboardMovement keys
  //KeyboardMovement.KeyUp = eKeyW;
  //KeyboardMovement.KeyDown = eKeyS;
  //KeyboardMovement.KeyLeft = eKeyA;
  //KeyboardMovement.KeyRight = eKeyD;

  // Auto-save on the save slot 999
  SetRestartPoint();
}

//=============================================================================
// Common GUI functions
//=============================================================================

// hide the icon bar and show a GUI
function open_gui(GUI* gui_to_open)
{
  if (gui_to_open != gInventory)
  {
    lblOverHotspot.Visible = false;
  }

  gIconbar.Visible = false;
  mouse.UseModeGraphic(eModePointer);
  gui_to_open.Visible = true;
}

// hide the GUI and show the icon bar
function close_gui(GUI* gui_to_close)
{
  gui_to_close.Visible = false;
  mouse.UseDefaultGraphic();
  lblOverHotspot.Visible = true;
  gIconbar.Visible = true;
}

// hide a GUI, based on a GUI control
function close_owning_gui(GUIControl* control)
{
  close_gui(control.OwningGUI);
}

// hide a GUI directly from an OnClick event
function close_gui_onclick(GUIControl *control, MouseButton button)
{
  close_owning_gui(control);
}

function show_inventory_window()
{
  mouse.Mode = eModeInteract;
  open_gui(gInventory);
}

function show_save_game_dialog()
{
  // get the list of save games
  lstSaveGamesList.FillSaveGameList();

  if (lstSaveGamesList.ItemCount > 0)
  {
    // if there is at least one, set the default text
    // to be the first game's name
    txtNewSaveName.Text = lstSaveGamesList.Items[0];
  }
  else
  {
    // no save games yet, so default to empty text
    txtNewSaveName.Text = "";
  }

  open_gui(gSaveGame);
}

function show_restore_game_dialog()
{
  lstRestoreGamesList.FillSaveGameList();
  open_gui(gRestoreGame);
}

//=============================================================================
// ICONBAR
//=============================================================================

function btnIconSave_Click(GUIControl *control, MouseButton button)
{
  show_save_game_dialog();
}

function btnIconLoad_Click(GUIControl *control, MouseButton button)
{
  show_restore_game_dialog();
}

function btnIconPanel_Click(GUIControl *control, MouseButton button)
{
  open_gui(gPanel);
}

function btnIconExit_Click(GUIControl *control, MouseButton button)
{
  open_gui(gExitGame);
}

//=============================================================================
// INVENTORY WINDOW
//=============================================================================

function btnInvUp_Click(GUIControl *control, MouseButton button)
{
  invCustom.ScrollUp();
}

function btnInvDown_Click(GUIControl *control, MouseButton button)
{
  invCustom.ScrollDown();
}

function btnInvSelect_Click(GUIControl *control, MouseButton button)
{
  // switch to the interact cursor
  mouse.Mode = eModeInteract;
  // ...but override the appearance to look like the arrow
  mouse.UseModeGraphic(eModePointer);
}

function btnIconInv_Click(GUIControl *control, MouseButton button)
{
  show_inventory_window();
}

function btnIconCurInv_Click(GUIControl *control, MouseButton button)
{
  if (player.ActiveInventory != null)
  {
    mouse.Mode = eModeUseinv;
  }
}

//=============================================================================
// CONTROL PANEL
//=============================================================================

function btnSave_OnClick(GUIControl *control, MouseButton button)
{
  close_owning_gui(control);
  Wait(1);
  btnIconSave_Click(btnIconSave, eMouseLeft);
}

function btnAbout_OnClick(GUIControl *control, MouseButton button)
{
  Display("%s\nAGS %s", Game.Name, System.Version);
}

function btnQuit_OnClick(GUIControl *control, MouseButton button)
{
  close_owning_gui(control);
  open_gui(gExitGame);
}

function btnLoad_OnClick(GUIControl *control, MouseButton button)
{
  close_owning_gui(control);
  Wait(1);
  btnIconLoad_Click(btnIconLoad, eMouseLeft);
}

function sldAudio_OnChange(GUIControl *control)
{
  System.Volume = sldAudio.Value;
}

function sldVoice_OnChange(GUIControl *control)
{
  SetSpeechVolume(sldVoice.Value);
}

function btnVoice_OnClick(GUIControl *control, MouseButton button)
{
  if (Speech.VoiceMode == eSpeechVoiceAndText)
  {
    Speech.VoiceMode = eSpeechVoiceOnly;
    btnVoice.Text = "Voice only";
  }
  else if (Speech.VoiceMode == eSpeechVoiceOnly)
  {
    Speech.VoiceMode = eSpeechTextOnly;
    btnVoice.Text = "Text only";
  }
  else if (Speech.VoiceMode == eSpeechTextOnly)
  {
    Speech.VoiceMode = eSpeechVoiceAndText;
    btnVoice.Text = "Voice and Text";
  }
}

function sldGamma_OnChange(GUIControl *control)
{
  System.Gamma = sldGamma.Value;
}

function btnDefault_OnClick(GUIControl *control, MouseButton button)
{
  set_default_options();
}

function sldSpeed_OnChange(GUIControl *control)
{
  SetGameSpeed(sldSpeed.Value);
}

//=============================================================================
// SAVE / LOAD DIALOGS
//=============================================================================

int find_save_slot(String name)
{
  bool slots[] = new bool[999];
  int slots_used = 0;

  // record which slots are occupied already, 
  // if the types save name matches any existing one, then use that
  for (int i = 0; i < lstSaveGamesList.ItemCount; i++)
  {
    if (lstSaveGamesList.Items[i] == name)
    {
      // found existing save with matching name
      return lstSaveGamesList.SaveGameSlots[i];
    }

    // remember which slots are already taken
    slots[lstSaveGamesList.SaveGameSlots[i]] = true;
    slots_used++;
  }
  
  // current version of AGS has a limit of 50 save slots
  // that may be displayed in the ListBox at the same time
  if (slots_used >= 50)
  {
    return -1;
  }

  // find first free save slot, starting with slot 1 (for "cosmetic" purposes)
  for (int i = 1; i < 999; i++)
  {
    if (!slots[i])
    {
      return i;
    }
  }

  // no free slots found
  return -1;
}

function btnSaveGame_OnClick(GUIControl *control, MouseButton button)
{
  int gameSlotToSaveInto = find_save_slot(txtNewSaveName.Text);

  if (gameSlotToSaveInto < 0)
  {
    Display("Save slots limit of 50 is reached, delete some of the existing saves first!");
  }
  else
  {
    SaveGameSlot(gameSlotToSaveInto, txtNewSaveName.Text);
    close_owning_gui(control);
  }
}

function btnRestoreGame_OnClick(GUIControl *control, MouseButton button)
{
  if (lstRestoreGamesList.SelectedIndex >= 0)
  {
    RestoreGameSlot(lstRestoreGamesList.SaveGameSlots[lstRestoreGamesList.SelectedIndex]);
  }

  close_owning_gui(control);
}

function lstSaveGamesList_OnSelectionCh(GUIControl *control)
{
  txtNewSaveName.Text = lstSaveGamesList.Items[lstSaveGamesList.SelectedIndex];
}

function txtNewSaveName_OnActivate(GUIControl *control)
{
  // pressing Return in the text box simulates clicking the save button
  btnSaveGame_OnClick(control, eMouseLeft);
}

function btnDeleteSave_OnClick(GUIControl *control, MouseButton button)
{
  if (lstSaveGamesList.SelectedIndex >= 0)
  {
    DeleteSaveSlot(lstSaveGamesList.SaveGameSlots[lstSaveGamesList.SelectedIndex]);
    lstSaveGamesList.FillSaveGameList();
  }
}

//=============================================================================
// RESTART DIALOG
//=============================================================================

function btnRestart_OnClick(GUIControl *control, MouseButton button)
{
  close_owning_gui(control);
  open_gui(gRestart);
}

function btnRestartYes_OnClick(GUIControl *control, MouseButton button)
{
  RestartGame();
}

//=============================================================================
// QUIT DIALOG
//=============================================================================

function btnConfirmedQuit_OnClick(GUIControl *control, MouseButton button)
{
  QuitGame(0);
}

//=============================================================================
// PLAYER INPUT
//=============================================================================

// called when a key is pressed
function on_key_press(eKeyCode keycode, int mod)
{
  // check for Escape first, so it can be used on GUIs that pause the game
  if (keycode == eKeyEscape)
  {
    if (gInventory.Visible)
    {
      // Escape will close the restore game dialog
      close_gui(gInventory);
    }
    else if (gPanel.Visible)
    {
      // Escape will turn the panel off
      close_gui(gPanel);
    }
    else if (gSaveGame.Visible)
    {
      // Escape will close the save game dialog
      close_gui(gSaveGame);
    }
    else if (gRestoreGame.Visible)
    {
      // Escape will close the restore game dialog
      close_gui(gRestoreGame);
    }
    if (gRestart.Visible)
    {
      // Escape will cancel a restart
      close_gui(gRestart);
    }
    else if (gExitGame.Visible)
    {
      // Escape will cancel exiting the game
      close_gui(gExitGame);
    }
    else
    {
      // Escape will turn the panel on
      open_gui(gPanel);
    }
  }
  else if (IsGamePaused() || !IsInterfaceEnabled())
  {
    // game paused, so don't react to any keypresses
    keycode = 0;
  }
  else if (keycode == eKeyQ && (mod & eKeyModCtrl))
  {
    // Ctrl-Q will quit the game
    open_gui(gExitGame);
  }
  else if (keycode == eKeyF5)
  {
    // F5 will prompt to save a game
    show_save_game_dialog();
  }
  else if (keycode == eKeyF7)
  {
    // F7 will prompt to save a game
    show_restore_game_dialog();
  }
  else if (keycode == eKeyF9)
  {
    // F9 will prompt to restart the game
    open_gui(gRestart);
  }
  else if (keycode == eKeyF12)
  {
    // F12 will save a screenshot to the save game folder
    SaveScreenShot("screenshot.pcx");
  }
  else if (keycode == eKeyReturn)
  {
    if (gRestart.Visible)
    {
      // Enter confirms a restart
      RestartGame();
    }
  }
  else if (keycode == eKeyTab)
  {
    // Tab opens the inventory
    show_inventory_window();
  }
  else if (mod & eKeyModCtrl)
  {
    if (keycode == eKeyS)
    {
      // Ctrl-S will give the player all defined inventory items
      Debug(0, 0);
    }
    else if (keycode == eKeyV)
    {
      // Ctrl-V will show game engine version and build date
      Debug(1, 0);
    }
    else if (keycode == eKeyA)
    {
      // Ctrl-A will show walkable areas
      Debug(2, 3);
    }
    else if (keycode == eKeyX)
    {
      // Ctrl-X will let the player teleport to any room
      Debug(3, 0);
    }
  }
  else if (keycode == eKeyM && startGame == 1)
  {
    if (gMap.Visible)
    {
      close_gui(gMap);
    }
    else
    {
      open_gui(gMap);
    }
  }
}

function handle_room_click(MouseButton button)
{
  if (button == eMouseLeft)
  {
    // left-click, so try using the current mouse cursor mode at this position
    Room.ProcessClick(mouse.x, mouse.y, mouse.Mode );
  }
  else if (button == eMouseRight || button == eMouseWheelSouth){
    // right-click or mouse wheel down will cycle the mouse cursor mode forwards
    mouse.SelectNextMode();
  }
  else if (button == eMouseMiddle)
  {
    // middle-click makes the character walk to clicked area, regardless of cursor mode
    Room.ProcessClick(mouse.x, mouse.y, eModeWalkto);
  }
  else if (button == eMouseWheelNorth)
  {
    // mouse wheel up will cycle the cursor mode backwards
    mouse.SelectPreviousMode();
  }
}

function handle_inventory_click(MouseButton button)
{
  // InventoryItem.GetAtScreenXY could return null here
  // so using game.inv_activated instead is a safer option
  InventoryItem* item = inventory[game.inv_activated];
  
  if (button == eMouseLeftInv)
  {
    if (mouse.Mode == eModeInteract)
    {
      // interact mode selects an inventory item
      player.ActiveInventory = item;
    }
    else if (mouse.Mode == eModeUseinv)
    {
      if (item.ID != player.ActiveInventory.ID)
      {
        // use one item on another
        item.RunInteraction(eModeUseinv);
      }
    }
    else
    {
      // otherwise run corresponding interaction (LookAt, etc)
      item.RunInteraction(mouse.Mode);
    }
  }
  else
  {
    // right click is always "Look At"
    item.RunInteraction(eModeLookat);
  }
}

// called when a mouse button is clicked
function on_mouse_click(MouseButton button)
{
  if (button == eMouseLeftInv || button == eMouseRightInv || button == eMouseMiddleInv)
  {
    handle_inventory_click(button);
  }
  // game is paused, then don't process mouse clicks inside the room
  else if (!IsGamePaused())
  {
    handle_room_click(button);
  }
}

//=============================================================================
// GAME EVENTS
//=============================================================================

// called on every game cycle, except when the game is blocked
function repeatedly_execute()
{
}

// called on every game cycle, even when the game is blocked
function repeatedly_execute_always()
{
}

// Called when a dialog script line "run-script" is processed
function dialog_request(int param)
{
}

// called when processclick wasn't handled anywhere else
function unhandled_event (int what, int type)
{
  if (what == 1) // Unhandled events for HOTSPOTS
  {
    if (type == 1) // look
    {
      player.Say("I see nothing special about it.");
    }
    else if (type == 2) // interact
    {
      player.Say("I can't do anything with it.");
    }
    else if (type == 3) // use inv on
    {
      player.Say("That won't do anything.");
    }
    else if (type == 4) // talk to
    {
      player.Say("I don't think it's going to talk back.");
    }
    else if (type == 7) // pick up
    {
      player.Say("I'm not sure I should be taking it.");
    }
  }
  else if (what == 2) // Unhandled events for OBJECTS
  {
    if (type == 0) // look
    {
      player.Say("Looks alright.");
    }
    else if (type == 1) // interact
    {
      player.Say("I'm not sure how to use it.");
    }
    else if (type == 2) // talk to
    {
      player.Say("I don't think it's going to answer me.");
    }
    else if (type == 3) // use inv on
    {
      player.Say("That's a funny idea.");
    }
    else if (type == 5) // pick up
    {
      player.Say("I don't want to have it.");
    }
  }
  else if (what == 3) // Unhandled events for CHARACTERS
  {
    if (type == 0) // look
    {
      player.Say("Hm.");
    }
    else if (type == 1) // interact
    {
      player.Say("Eh?");
    }
    else if (type == 2) // talk to
    {
      player.Say("Got nothing to say.");
    }
    else if (type == 3) // use inv on
    {
      player.Say("I don't think I should give that away.");
    }
    else if (type == 5) // pick up
    {
      player.Say("I'm not sure they would be compliant.");
    }
  }
  else if (what == 5) // Unhandled events for INVENTORY ITEMS
  {
    if (type == 0) // look
    {
      player.Say("It's just some junk in my inventory.");
    }
    else if (type == 1) // interact
    {
      player.Say("Er, no?");
    }
    else if (type == 3) // use inv on
    {
      player.Say("That's ridiculous.");
    }
  }
}

//=============================================================================
// Global interactions: Characters, Inventory items, ...
//=============================================================================

function cDet_Look(Character *thisCharacter, CursorMode mode)
{
  Display("Damn, I'm looking good!");
}

function cDet_Interact(Character *thisCharacter, CursorMode mode)
{
  Display("You rub your hands up and down your clothes.");
}

function cDet_Talk(Character *thisCharacter, CursorMode mode)
{
  if(progress == 1)
  {
    if (samples == 0)
    {
      cDet.Say("Officer Tom wants me to go to the beach");
    }
    else
    {
      cDet.Say("I should try to find more clues on the beach");
    }
  }
  else if(progress == 2)
  {
    cDet.Say("I should go back to Officer Tom");
  }
  else if(progress == 3)
  {
    cDet.Say("I should tell Officer Tom about this");
  }
  else if (progress == 4)
  {
    if (johnsonProgress == 0)
    {
      cDet.Say("I should start by talking to the Johnsons");
    }
    else if (johnsonProgress == 1)
    {
      if (fishermanProgress == 0)
      {
        cDet.Say("I wonder if the fisherman saw anything on the beach");
      }
      else if (fishermanProgress == 1)
      {
        if (foundSafe == false)
        {
          if (andrewProgress == 0)
          {
            cDet.Say("Maybe I should focus on the door on the papers");
          }
          else
          {
            cDet.Say("I need to see if anyone has a safe like the one on the paper");
          }
        }
        else 
        {
          cDet.Say("I need to ask the fisherman whats in the safe");
        }
      }
      else
      {
        if (tapesCollected == false)
        {
          cDet.Say("I need to go back to the fishermans shack to get the tape");
        }
        else if (tapesWatched == false)
        {
          cDet.Say("I need to go back to the police department to watch the tape");
        }
      }
    }
  }
  else if (progress == 5)
  {
    if (robertIntro == 3)
    {
      cDet.Say("I need to talk to Mandy about cheating on Mark");
    }
    else
    {
      cDet.Say("I need to ask Robert about this");
    }
  }
  else if (progress == 6)
  {
    cDet.Say("I should talk to Officer Tom and conclude this case");
  }
}

//===========================================================================
// Puzzle Piece Combinations
//===========================================================================

//Single Piece Combinations

function iTopLeftPuzzle_UseInv(InventoryItem *theItem, CursorMode mode)
{
  if (player.ActiveInventory == iBottomLeftPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iTopLeftPuzzle);
    player.LoseInventory(iBottomLeftPuzzle);
    player.AddInventory(iLeftPieces);
  }
  
  if (player.ActiveInventory == iTopRightPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iTopLeftPuzzle);
    player.LoseInventory(iTopRightPuzzle);
    player.AddInventory(iTopPieces);
  }
  
  if (player.ActiveInventory == iBottomRightPuzzle)
  {
    Display("Hmm. These pieces don't fit together");
  }
  
  if (player.ActiveInventory == iBottomPieces)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iTopLeftPuzzle);
    player.LoseInventory(iBottomPieces);
    player.AddInventory(iMissingTR);
  }
  
  if (player.ActiveInventory == iRightPieces)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iTopLeftPuzzle);
    player.LoseInventory(iRightPieces);
    player.AddInventory(iMissingBL);
  }
  
  if (player.ActiveInventory == iMissingTL)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iTopLeftPuzzle);
    player.LoseInventory(iMissingTL);
    player.AddInventory(iFullPaper);
    progress = progress + 1;
    paperCode = true;
  }
}

function iTopRightPuzzle_UseInv(InventoryItem *theItem, CursorMode mode)
{
  if (player.ActiveInventory == iBottomRightPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iTopRightPuzzle);
    player.LoseInventory(iBottomRightPuzzle);
    player.AddInventory(iRightPieces);
  }
  
  if (player.ActiveInventory == iTopLeftPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iTopLeftPuzzle);
    player.LoseInventory(iTopRightPuzzle);
    player.AddInventory(iTopPieces);
  }
  
  if (player.ActiveInventory == iBottomLeftPuzzle)
  {
    Display("Hmm. These pieces don't fit together");
  }
  
  if (player.ActiveInventory == iBottomPieces)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iTopRightPuzzle);
    player.LoseInventory(iBottomPieces);
    player.AddInventory(iMissingTL);
  }
  
  if (player.ActiveInventory == iLeftPieces)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iTopRightPuzzle);
    player.LoseInventory(iLeftPieces);
    player.AddInventory(iMissingBR);
  }
  
  if (player.ActiveInventory == iMissingTR)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iTopRightPuzzle);
    player.LoseInventory(iMissingTR);
    player.AddInventory(iFullPaper);
    progress = progress + 1;
    paperCode = true;
  }
}

function iBottomLeftPuzzle_UseInv(InventoryItem *theItem, CursorMode mode)
{
  if (player.ActiveInventory == iTopLeftPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iTopLeftPuzzle);
    player.LoseInventory(iBottomLeftPuzzle);
    player.AddInventory(iLeftPieces);
  }
  
  if (player.ActiveInventory == iBottomRightPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iBottomLeftPuzzle);
    player.LoseInventory(iBottomRightPuzzle);
    player.AddInventory(iBottomPieces);
  }
  
  if (player.ActiveInventory == iTopRightPuzzle)
  {
    Display("Hmm. These pieces don't fit together");
  }
  
  if (player.ActiveInventory == iTopPieces)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iBottomLeftPuzzle);
    player.LoseInventory(iTopPieces);
    player.AddInventory(iMissingBR);
  }
  
  if (player.ActiveInventory == iRightPieces)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iBottomLeftPuzzle);
    player.LoseInventory(iRightPieces);
    player.AddInventory(iMissingTL);
  }
  
  if (player.ActiveInventory == iMissingBL)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iBottomLeftPuzzle);
    player.LoseInventory(iMissingBL);
    player.AddInventory(iFullPaper);
    progress = progress + 1;
    paperCode = true;
  }
}

function iBottomRightPuzzle_UseInv(InventoryItem *theItem, CursorMode mode)
{
  if (player.ActiveInventory == iTopRightPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iTopRightPuzzle);
    player.LoseInventory(iBottomRightPuzzle);
    player.AddInventory(iRightPieces);
  }
  
  if (player.ActiveInventory == iBottomLeftPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iBottomLeftPuzzle);
    player.LoseInventory(iBottomRightPuzzle);
    player.AddInventory(iBottomPieces);
  }
  
  if (player.ActiveInventory == iTopLeftPuzzle)
  {
    Display("Hmm. These pieces don't fit together");
  }
  
  if (player.ActiveInventory == iTopPieces)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iBottomRightPuzzle);
    player.LoseInventory(iTopPieces);
    player.AddInventory(iMissingBL);
  }
  
  if (player.ActiveInventory == iLeftPieces)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iBottomRightPuzzle);
    player.LoseInventory(iLeftPieces);
    player.AddInventory(iMissingTR);
  }
  
  if (player.ActiveInventory == iMissingBR)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iBottomRightPuzzle);
    player.LoseInventory(iMissingBR);
    player.AddInventory(iFullPaper);
    progress = progress + 1;
    paperCode = true;
  }
}

//Two Piece Combinations

function iTopPieces_UseInv(InventoryItem *theItem, CursorMode mode)
{
  if (player.ActiveInventory == iBottomLeftPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iTopPieces);
    player.LoseInventory(iBottomLeftPuzzle);
    player.AddInventory(iMissingBR);
  }
  
  if (player.ActiveInventory == iBottomRightPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iTopPieces);
    player.LoseInventory(iBottomRightPuzzle);
    player.AddInventory(iMissingBL);
  }
  
  if (player.ActiveInventory == iBottomPieces)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iTopPieces);
    player.LoseInventory(iBottomPieces);
    player.AddInventory(iFullPaper);
    progress = progress + 1;
    paperCode = true;
  }
}

function iBottomPieces_UseInv(InventoryItem *theItem, CursorMode mode)
{
  if (player.ActiveInventory == iTopLeftPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iBottomPieces);
    player.LoseInventory(iTopLeftPuzzle);
    player.AddInventory(iMissingTR);
  }
  
  if (player.ActiveInventory == iTopRightPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iBottomPieces);
    player.LoseInventory(iTopRightPuzzle);
    player.AddInventory(iMissingTL);
  }
  
  if (player.ActiveInventory == iTopPieces)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iTopPieces);
    player.LoseInventory(iBottomPieces);
    player.AddInventory(iFullPaper);
    progress = progress + 1;
    paperCode = true;
  } 
}

function iLeftPieces_UseInv(InventoryItem *theItem, CursorMode mode)
{
  if (player.ActiveInventory == iTopRightPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iLeftPieces);
    player.LoseInventory(iTopRightPuzzle);
    player.AddInventory(iMissingBR);
  }
  
  if (player.ActiveInventory == iBottomRightPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iLeftPieces);
    player.LoseInventory(iBottomRightPuzzle);
    player.AddInventory(iMissingTR);
  }
  
  if (player.ActiveInventory == iRightPieces)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iLeftPieces);
    player.LoseInventory(iRightPieces);
    player.AddInventory(iFullPaper);
    progress = progress + 1;
    paperCode = true;
  }
}

function iRightPieces_UseInv(InventoryItem *theItem, CursorMode mode)
{
  if (player.ActiveInventory == iTopLeftPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iRightPieces);
    player.LoseInventory(iTopLeftPuzzle);
    player.AddInventory(iMissingBL);
  }
  
  if (player.ActiveInventory == iBottomLeftPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iRightPieces);
    player.LoseInventory(iBottomLeftPuzzle);
    player.AddInventory(iMissingTL);
  }
  
  if (player.ActiveInventory == iLeftPieces)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iLeftPieces);
    player.LoseInventory(iRightPieces);
    player.AddInventory(iFullPaper);
    progress = progress + 1;
    paperCode = true;
  }
}

//Three Piece Combinations

function iMissingTL_UseInv(InventoryItem *theItem, CursorMode mode)
{
  if (player.ActiveInventory == iTopLeftPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iMissingTL);
    player.LoseInventory(iTopLeftPuzzle);
    player.AddInventory(iFullPaper);
    progress = progress + 1;
    paperCode = true;
  }
}

function iMissingBL_UseInv(InventoryItem *theItem, CursorMode mode)
{
  if (player.ActiveInventory == iBottomLeftPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iMissingBL);
    player.LoseInventory(iBottomLeftPuzzle);
    player.AddInventory(iFullPaper);
    progress = progress + 1;
    paperCode = true;
  }
}

function iMissingTR_UseInv(InventoryItem *theItem, CursorMode mode)
{
  if (player.ActiveInventory == iTopRightPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iMissingTR);
    player.LoseInventory(iTopRightPuzzle);
    player.AddInventory(iFullPaper);
    progress = progress + 1;
    paperCode = true;
  }
}

function iMissingBR_UseInv(InventoryItem *theItem, CursorMode mode)
{
  if (player.ActiveInventory == iBottomRightPuzzle)
  {
    Display("You taped the pieces together");
    player.LoseInventory(iMissingBR);
    player.LoseInventory(iBottomRightPuzzle);
    player.AddInventory(iFullPaper);
    progress = progress + 1;
    paperCode = true;
  }
}

//=========================================================
//Police Talk Interactions
//=========================================================

function cPolice_Talk(Character *theCharacter, CursorMode mode)
{
  if (progress == 1)
  {
    cPolice.Say("Go to the beach and LOOK for clues");
  }
  else if (progress == 2)
  {
    cPolice.Say("Put the papers from your inventory and SEE what it means");
  }
  else if (progress == 3)
  {
    dPaper.Start();
    progress = progress + 1;
  }
  else if (progress == 4)
  {
    cPolice.Say("Ask the townspeople if they know anything");
  }
  else if (progress == 5)
  {
    if (robertIntro == 2)
    {
      dJail.Start();
      robertIntro = 3;
    }
    else
    {
      cPolice.Say("Go talk to Mandy or Mark NOW");
    }
  }
  else if (progress == 6)
  {
    dConclusion.Start();
    cDet.ChangeRoom(9, 400, 0);
  }
}

//===========================================================
//Background Musics
//===========================================================

function UpdateMusic()
{
  if (cDet.Room == 1)
  {
    backMusic = aGotothePicnic.Play();
  }
}

//==========================================================
//Mark and Mandy Talk Interactions
//==========================================================

function cMark_Talk(Character *theCharacter, CursorMode mode)
{
  if (johnsonProgress < 1)
  {
    dJohnsonIntro.Start();
    johnsonProgress = 1;
  }
  else
  {
    if (robertIntro == 3)
    {
      cDet.Walk(221, 124, eBlock, eWalkableAreas);
      dMarkCheat.Start();
      progress = 6;
    }
    else
    {
      cMark.Say("Any luck?");
      cDet.Say("A little. Still working on it though");
    }
  }
}

function cMandy_Talk(Character *theCharacter, CursorMode mode)
{
  if (johnsonProgress < 1)
  {
    dJohnsonIntro.Start();
    johnsonProgress = 1;
  }
  else
  {
    if (robertIntro == 3)
    {
      cDet.Walk(87, 124, eBlock, eWalkableAreas);
      dMandyCheat.Start();
      progress = 6;
    }
    else
    {
      cMandy.Say("Did you find who murdered *sniff* our son?");
      cDet.Say("Not yet. Don't worry though I'll figure it out soon");
    }
  }
}

//==========================================================
//Fisherman Talk Interaction
//==========================================================

function cFisherman_Talk(Character *theCharacter, CursorMode mode)
{
  if (johnsonProgress < 1)
  {
    cDet.Walk(210, 150, eBlock, eWalkableAreas);
    cDet.Say("How's it goin");
    cFisherman.Say("Couple bites so far");
    cDet.Say("Good luck");
  }
  else
  {
    cDet.Walk(210, 150, eBlock, eWalkableAreas);
    if (fishermanProgress < 1)
    {
      dFisherIntro.Start();
      fishermanProgress = 1;
    }
    else
    {
      if (foundSafe == false)
      {
        cDet.Say("Wh--");
        cFisherman.Say("Can you leave me alone");
        cDet.Say("Fine");
      }
    }
  }
  
  if (foundSafe == true)
  {
    if (fishermanProgress == 1)
    {
      dSafe.Start();
      fishermanProgress = 2;
    }
    else if (fishermanProgress == 2)
    {
      cFisherman.Say("Did the tape help?");
      if (tapesCollected == false)
      {
        cDet.Say("Still haven't gotten it yet");
      }
      else
      {
        if (tapesWatched == false)
        {
          cDet.Say("Haven't watched it yet");
        }
        else
        {
          cDet.Say("Yes a lot. Thanks");
        }
      }
    }
  }
}

//==========================================================
//Sandra Talk Interaction
//==========================================================

function cSandra_Talk(Character *theCharacter, CursorMode mode)
{
  if (sandraIntro == 0)
  {
    dSandra.Start();
    sandraIntro = 1;
  }
  else
  {
    cSandra.Say("Any luck on the case?");
    cDet.Say("Still working on it");
  }
}

//==========================================================
//Andrew Talk Interaction
//==========================================================

function cAndrew_Talk(Character *theCharacter, CursorMode mode)
{
  if (paperCode == true)
  {
    if (foundSafe == false)
    {
      dBensonDoor.Start();
      andrewProgress = 1;
    }
  }
  
  if (andrewProgress == 1)
  {
    if (foundSafe == true)
    {
      if (fishermanProgress == 2)
      {
        cDet.Say("You were right about the safe");
        cAndrew.Say("Awesome. Glad I could help");
      }
    }
  }
  else
  {
    cDet.Say("How's it going?");
    cAndrew.Say("Pretty well");
  }
}

//==========================================================
//Robert Talk Interaction
//==========================================================

function cRobert_Talk(Character *theCharacter, CursorMode mode)
{
  if (tapesWatched == false)
  {
    if (robertIntro == 0)
    {
      dRobert.Start();
      robertIntro = 1;
    }
    else
    {
      cRobert.Say("Any luck on the case?");
      cDet.Say("Still working on it");
    }
  }
  else if (tapesWatched == true && robertIntro < 2)
  {
    if (robertIntro == 0)
    {
      dTapeRobert.Start();
    }
    else
    {
      dRobertLie.Start();
    }
    cDet.ChangeRoom(2, 67, 142, eDirectionDown);
    robertIntro = 2;
  }
  else
  {
    if (robertIntro == 2)
    {
      dJail.Start();
      robertIntro = 3;
    }
  }
}

//=============================================================
// GUI Functions
//=============================================================

function Button1_OnClick(GUIControl *control, MouseButton button)
{
  close_gui(gStartup);
  open_gui(gStartup2);
}

function Button6_OnClick(GUIControl *control, MouseButton button)
{
  close_gui(gStartup2);
  open_gui(gStartup3);
}

function Button7_OnClick(GUIControl *control, MouseButton button)
{
  close_gui(gStartup3);
  cDet.ChangeRoom(2);
  startGame = 1;
}

